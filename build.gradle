plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.elovirta'
version = '1.0.0'
description = 'DITA-OT LSP'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.21.1'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'com.elovirta.dita.DitaLanguageServer'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('generateVersionProperties') {
    doLast {
        def propsFile = new File(layout.buildDirectory.get().asFile, "resources/main/version.properties")
        propsFile.parentFile.mkdirs()

        def props = new Properties()
        props.setProperty('version', version.toString())
        props.setProperty('description', project.description)

        propsFile.withWriter { writer ->
            props.store(writer, 'Application version information')
        }
    }
}
tasks.named('processResources') {
    dependsOn('generateVersionProperties')
}

//// Create a fat JAR with all dependencies
//tasks.register('fatJar', Jar) {
//    archiveClassifier = 'all'
//    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
//    manifest {
//        attributes 'Main-Class': 'com.elovirta.dita.DitaLanguageServer'
//    }
//    from {
//        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    }
//    with jar
//}

shadowJar {
    archiveBaseName.set('dita-language-server')
    archiveClassifier.set('all')
    archiveVersion.set('1.0.0')
    manifest {
        attributes 'Main-Class': 'com.example.dita.DitaLanguageServer'
    }
}
