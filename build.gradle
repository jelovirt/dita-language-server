plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.elovirta'
version = '0.1.0'
description = 'DITA Language Server'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.21.1'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.2'
    implementation group: 'xml-apis', name: 'xml-apis', version: '1.4.01'
    implementation group: 'org.xmlresolver', name: 'xmlresolver', version: '6.0.19'
    implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: '12.7'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'com.elovirta.dita.DitaLanguageServer'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('generateVersionProperties') {
    doLast {
        def propsFile = new File(layout.buildDirectory.get().asFile, "resources/main/version.properties")
        propsFile.parentFile.mkdirs()

        def props = new Properties()
        props.setProperty('version', version.toString())
        props.setProperty('description', project.description)

        propsFile.withWriter { writer ->
            props.store(writer, 'Application version information')
        }
    }
}
tasks.named('processResources') {
    dependsOn('generateVersionProperties')
}

shadowJar {
    archiveBaseName.set('dita-language-server')
    archiveClassifier.set('all')
    archiveVersion.set('1.0.0')
    manifest {
        attributes 'Main-Class': 'com.example.dita.DitaLanguageServer'
    }
}

spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
        targetExclude 'build/**'
    }
}