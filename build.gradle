buildscript {
    repositories {
        mavenCentral()
        maven { url "https://dev.saxonica.com/maven" }
    }
    dependencies {
        classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: '12.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.nwalsh.gradle.saxon.saxon-gradle' version '0.10.7'
}

group = 'com.elovirta'
version = '0.1.0'
description = 'DITA Language Server'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.24.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.27'
    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'xml-apis:xml-apis:1.4.01'
    implementation 'net.sf.saxon:Saxon-HE:12.9'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'com.elovirta.dita.DitaLanguageServer'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('generateVersionProperties') {
    doLast {
        def propsFile = new File(layout.buildDirectory.get().asFile, "resources/main/version.properties")
        propsFile.parentFile.mkdirs()

        def props = new Properties()
        props.setProperty('version', version.toString())
        props.setProperty('description', project.description)

        propsFile.withWriter { writer ->
            props.store(writer, 'Application version information')
        }
    }
}

tasks.register('copyTranspileXsl', Copy) {
    from 'schxslt2/src/main/resources/content/transpile.xsl'
    into layout.buildDirectory.dir('resources/main/xslt')
}

tasks.named('processResources') {
    dependsOn 'generateVersionProperties', 'copyTranspileXsl'
}

shadowJar {
    archiveClassifier.set('all')
    archiveVersion.set(project.version.toString())
    manifest {
        attributes 'Main-Class': 'com.example.dita.DitaLanguageServer'
    }
}

spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
        targetExclude 'build/**'
    }
}